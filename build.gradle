plugins {
    id 'de.gliderpilot.semantic-release' version '1.4.0'
    id "com.palantir.docker" version "0.21.0"
    id "org.jetbrains.kotlin.jvm" version "1.3.11"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.11"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.11"
    id "org.springframework.boot" version "2.1.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

repositories { mavenCentral() }

apply plugin: 'maven-publish'

group = "com.ddubson.versioning"

test { useJUnitPlatform() }

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.2"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.3.2"
}

project.ext.ghToken = project.hasProperty('ghToken')
        ? project.getProperty('ghToken') : System.getenv('GH_TOKEN') ?: null

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    dockerfile file("docker/Dockerfile")
    tag "dockerhub", "ddubson/artifact-versioning-example:${project.version}"
    name "docker.io/ddubson/artifact-versioning-example:${project.version}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}

semanticRelease {
    repo {
        releaseAsset jar
        ghToken = project.ghToken
    }
}

compileKotlin { kotlinOptions { jvmTarget = "1.8" }}
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" }}